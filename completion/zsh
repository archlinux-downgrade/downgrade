#compdef downgrade

_downgrade(){
  _arguments -S -C '*:packages:->pkg' \
             '--pacman[Specify pacman command]:pacman command' \
             '--pacman-conf[Specify pacman configuration file]:pacman config file:_files' \
             '--pacman-cache[Specify pacman cache directory or directories]:pacman cache directory:_path_files -/' \
             '--pacman-log[Specify pacman log-file]:pacman log file:_files' \
             '--maxdepth[Specify maximum depth for local cache find]:maximum search depth' \
             '--ala-url[Specify location of ALA server]:ala url' \
             '--ala-only[Option enables only ALA server search]' \
             '--cached-only[Option enables only cached search]' \
             '--nosudo[Option enables only su even when sudo is present]' \
             {-h,--help}'[Show help]'

  if [[ $state == "pkg" ]] && [[ ! $words =~ "\s\-\-\s" ]]; then
    local -a packages
    packages=($(pacman -Qsq "^$words[current]" 2>/dev/null))
    _describe 'packages' packages
  fi
}

_downgrade "$@"
# vim: syntax=Shell
